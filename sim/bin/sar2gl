#!/usr/bin/perl -w
# $Id: sar2gl,v 2.0 2002/09/22 02:07:28 tramm Exp $
#
# Convert a SAR aircraft file into OpenGL code
#

use strict;

my %functions;

print <<"";
/*****************************************************
 * \$Id\$
 * 
 * Models are converted from "Search and Rescue" v3d files
 * The graphics and 3d models in SAR are (C) 1999-2002 Taura Milana.
 *
 * Conversion by sar2gl, (c) Trammell Hudson
 *
 * More info:
 *
 *	http://autopilot.sourceforge.net/
 *
 */

while(<>)
{
	if( /^begin_quads/ )
	{
		$_ = "glBegin( GL_QUADS );\n";
		next;
	}

	if( /^begin_triangles/ )
	{
		$_ = "glBegin( GL_TRIANGLES );\n";
		next;
	}

	if( /^begin_triangle_fan/ )
	{
		$_ = "glBegin( GL_TRIANGLE_FAN );\n";
		next;
	}

	if( /^end_quads/ or /^end_triangles/ or /^end_triangle_fan/ )
	{
		$_ = "glEnd();\n";
		next;
	}

	if( /^begin_model (.*)/ )
	{
		my $n = $functions{$1}++;
		$_ = "void do_${1}_$n( void )\n{\n";
		next;
	}

	if( /^end_model .*/ )
	{
		$_ = "}\n";
		next;
	}

	# Convert a list of numbers into a comma separated list
	s/([.0-9])\s+([-+0-9])/$1, $2/g;

	if( s/^\s+normal (.*)$/	glNormal3f( $1 );/ )
	{
		next;
	}

	if( s/^\s*color (.*)$/ do_color( $1 );/ )
	{
		next;
	}

	if( s/^\s+([-+0-9].*)/	glVertex3f( $1 );/ )
	{
		next;
	}

	$_ = "// $_";

} continue {
	print;
}
